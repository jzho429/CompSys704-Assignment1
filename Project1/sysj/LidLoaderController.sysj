Controller(
		input signal pusherRetracted,pusherExtended,WPgripped,armAtSource,armAtDest,empty,request;
		output signal pusherExtend,vacOn,armSource,armDest;
		input Integer signal mode;
		input signal pusherExtendM,vacOnM,armSourceM,armDestM;
		)
->
{		
	signal selectM, selectA;
	
	{//Manual
		while(true){
			await(selectM);
			abort(selectA){
				{while(true){present(pusherExtendM){emit pusherExtend;} pause;}}
				||
				{while(true){present(vacOnM){emit vacOn;} pause;}}
				||
				{while(true){present(armSourceM){emit armSource;} pause;}}
				||
				{while(true){present(armDestM){emit armDest;} pause;}}
			}
		}
	}	
	||
	{//Auto
		while(true){
			await(selectA);
			abort(selectM){
				await(request && !empty);
				abort(pusherExtended){
					sustain pusherExtend;
				}
				pause;
				await(pusherRetracted);
				abort(armAtSource){
					sustain armSource;
				}
				pause;
				abort(armAtDest){
					{sustain vacOn;}
					||
					{await (WPgripped);
					sustain armDest;}
				}
			}
		}
	}
	||
	{//Mode select
		await(mode);
		while(true){
			if((Integer)#mode == 1){
				emit selectM;
			}
			else{
				emit selectA;
			}
			pause;
		}
	}
}