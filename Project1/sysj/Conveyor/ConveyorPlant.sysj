ConveyorPlant(
		input signal motConveyorOnOff, rotaryTableTrigger, bottleAtPos0, RTPutBottleAt2, RTPutBottleAt3, RTPutBottleAt4, RTPutBottleAt5; //Input
		output signal bottleAtPos1, bottleAtPos2, bottleAtPos3, bottleAtPos4, bottleAtPos5, bottleLeftPos5; //Output to Controller
		output signal bottleAtPos1E, bottleAtPos2E, bottleAtPos3E, bottleAtPos4E, bottleAtPos5E, bottleLeftPos5E, motorOn, bottleLeftPos5TL;//Output to GUI
		output signal bottleAtPos1RT, bottleAtPos2RT, bottleAtPos3RT, bottleAtPos4RT, bottleAtPos5RT; //Output to RotaryTable
		)
->{
	/*
	* Bottle positions
	* 0 - at Pos0 - Loading
	* 1 = at Pos1
	* 2 = at Pos2
	* 3 = at Pos3
	* 4 = at Pos4
	* 5 = at Pos5
	*/
	{
		loop { //Handles ConveyorController Signals
			abort(immediate (motConveyorOnOff)){
				System.out.println("ConveyorPlant: Conveyor Off");
				while(true){pause;}
			}
			abort(immediate (!motConveyorOnOff)){
				System.out.println("ConveyorPlant: Conveyor On");
				sustain motorOn;
			}
			pause;
		}
	}
	|| 
	/*
	 * check pos0, move to pos 1 if nothing there
	 * ||check pos1, move to pos 2 if rotary table ticks
	 * ||check pos3, move to pos 4 if rotary table ticks
	 * ||check pos4, move to pos 5 if rotary table ticks
	 * check pos5, move to bottleLeftPos5
	 */
	{
		{
			while(true){
				System.out.println("ConveyorPlant: Waiting for bottle at pos 0");
				await(bottleAtPos0 && motorOn);
				System.out.println("ConveyorPlant: Bottle at pos 0");
				//await(motorOn);
				abort(rotaryTableTrigger){
					System.out.println("Bottle at pos 1");
					sustain bottleAtPos1;
				}
				pause;
			}
		}
		||
		{
			while(true) {
				present(RTPutBottleAt2){
					System.out.println("Bottle at pre pos 2");
					abort(bottleAtPos3){
						System.out.println("Bottle at pos 2");
						sustain bottleAtPos2;
					}
				}
				pause;
			}
		}
		||
		{
			while(true) {
				present(RTPutBottleAt3){
					abort(bottleAtPos4){
						System.out.println("Bottle at pos 3");
						sustain bottleAtPos3;
					}
				}
				pause;
			}
		}
		||
		{
			while(true) {
				present(RTPutBottleAt4){
					abort(bottleAtPos5){
						System.out.println("Bottle at pos 4");
						sustain bottleAtPos4;
					}
				}
				pause;
			}
		}
		||
		{
			while(true){
				present(RTPutBottleAt5){
					abort(bottleLeftPos5){
						System.out.println("Bottle at pos 5");
						sustain bottleAtPos5;
					}
				}
				pause;
			}
		}
		||
		{
			while(true) {
				present(bottleAtPos5 && motorOn)
				{
					System.out.println("Bottle left pos 5");
					emit bottleLeftPos5;
				}
				pause;
			}
		}

	}
	||
	{
		//To VIZ
		{while(true){present(bottleAtPos1){emit bottleAtPos1E;} pause;}}
		||
		{while(true){present(bottleAtPos2){emit bottleAtPos2E;} pause;}}
		||
		{while(true){present(bottleAtPos3){emit bottleAtPos3E;} pause;}}
		||
		{while(true){present(bottleAtPos4){emit bottleAtPos4E;} pause;}}
		||
		{while(true){present(bottleAtPos5){emit bottleAtPos5E;} pause;}}
		||
		{while(true){present(bottleLeftPos5){emit bottleLeftPos5E;} pause;}}
		||
		//To Rotary Table
		{while(true){present(bottleAtPos1){emit bottleAtPos1RT;} pause;}}
		||
		{while(true){present(bottleAtPos2){emit bottleAtPos2RT;} pause;}}
		||
		{while(true){present(bottleAtPos3){emit bottleAtPos3RT;} pause;}}
		||
		{while(true){present(bottleAtPos4){emit bottleAtPos4RT;} pause;}}
		||
		{while(true){present(bottleAtPos5){emit bottleAtPos5RT;} pause;}}
	}
}