LidLoaderController(
		input signal pusherRetracted,pusherExtended,WPgripped,armAtSource,armAtDest; //,empty,request;
		input signal bottleAtPos3;
		output signal pusherExtend,vacOn,armSource,armDest;
		output signal doneAtPos3;
		)
->
{
	while(true){
		System.out.println("LidLoaderController: Waiting for bottleAtPos3");
		await(bottleAtPos3);
		System.out.println("LidLoaderController: bottleAtPos3");
		abort(pusherExtended){
			sustain pusherExtend;
		}
		pause;
		await(pusherRetracted);
		abort(armAtSource){
			sustain armSource;
		}
		System.out.println("LidLoaderController: armAtSource");
		pause;
		abort(armAtDest){
			{sustain vacOn;}
			||
			{await (WPgripped);
				System.out.println("LidLoaderController: WPgripped");
				sustain armDest;}
		}
		System.out.println("LidLoaderController: armAtDest");
		System.out.println("LidLoaderController: Done");
		emit doneAtPos3;
		pause;
	}
}